buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.moc.chitchat"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        uiTests {
            resValue "string", "server_url", "http://10.0.2.2:4000"
        }
        development {
            resValue "string", "server_url", "http://10.0.2.2:4000"
        }
        beta {
            resValue "string", "server_url", "https://beta.chitchat.monarchsofcofing.com"
        }
        production {
            resValue "string", "server_url", "https://chitchat.monarchsofcoding.com"
        }
    }
    compileOptions.incremental = false
    testOptions {
//        ,
//
//
//
//
// .returnDefaultValues = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.google.dagger:dagger:2.9'
    apt "com.google.dagger:dagger-compiler:2.9"
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.github.eoinsha:JavaPhoenixChannels:0.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.9'
    compile 'com.android.support:design:25.1.1'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.7.5"
    testCompile 'org.json:json:20160810'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "org.mockito:mockito-core:1.10.19"
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jacoco {
    toolVersion = "0.7.8"
    reportsDir = file("$buildDir/JacocoCoverageReport")
}

jacocoAndroidUnitTestReport {
    excludes += ['com/moc/chitchat/activity/**',
                 'com/moc/chitchat/application/**',
                 'com/moc/chitchat/client/**',
                 'com/moc/chitchat/service/**',
                 'com/moc/chitchat/controller/*_Factory.class',
                 'com/moc/chitchat/ApplicationModule.class',
                 'com/moc/chitchat/ChitChatApplication.class']
}

apply plugin: 'checkstyle'
checkstyle {
    toolVersion = "7.4"
}

task checkstyle (type: Checkstyle) {
    description 'Runs Checkstyle inspection against Android sourcesets.'
    group = 'Code Quality'

    ignoreFailures = false
    showViolations = false
    source 'src/main', 'src/release', 'src/androidTest/java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    reports {
        xml.destination "$project.buildDir/reports/checkstyle/checkstyle-report.xml"
    }
    classpath = files()
    configFile = file("config/checkstyle.xml")

    configProperties.lineWidth = 120
    showViolations = true
}

task checkstyleReport << {
    if (file("$buildDir/reports/checkstyle/checkstyle-report.xml").exists()) {
        ant.xslt(in: "$project.buildDir/reports/checkstyle/checkstyle-report.xml",
                style:"config/checkstyle-noframes-sorted.xsl",
                out:"$project.buildDir/reports/checkstyle/checkstyle-report.html"
        )
    }
}

gradle.taskGraph.afterTask {Task task, TaskState state ->
    if (task.name in ['checkstyle']) {
        checkstyleReport.execute()
    }
}
